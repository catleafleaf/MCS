#version 430

precision OVERWRITE_PRECISION float;
precision highp int;

layout(local_size_x = RESET_VALUE, local_size_y = 8, local_size_z = 1) in;

subroutine void formatPickerStore(in ivec2 coord, in vec4 result);
subroutine uniform formatPickerStore formatPickerStoreState;

layout(binding = 0, rgba16i) uniform readonly iimage2D coordMap;
layout(binding = 1, r8) uniform writeonly image2D resultMap;
layout(binding = 2, r16) uniform writeonly image2D resultMapR16;

uniform ivec2 size;
uniform vec2 preMultiply;

subroutine(formatPickerStore) void bit8Store(in ivec2 coord, in vec4 result) {
    imageStore(resultMap, coord, result);
}

subroutine(formatPickerStore) void bit16Store(in ivec2 coord, in vec4 result) {
    imageStore(resultMapR16, coord, result);
}

void main()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(coord, size))) return;
    vec4 result = vec4(max(imageLoad(coordMap, coord) - 1, 0) - coord.xyxy);
    float sdf = clamp(length(result.xy) * preMultiply.y, 0.0, 1.0) - clamp(length(result.zw) * preMultiply.x, 0.0, 1.0);
    result = vec4(clamp(sdf * 0.5 + 0.5, 0.0, 1.0), vec3(0.0));
    formatPickerStoreState(coord, result);
}